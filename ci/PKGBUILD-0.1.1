# Maintainer: Eric Zhao <21zhaoe@protonmail.com>

pkgname=cchmod
pkgver=0.1.1
pkgrel=2
pkgdesc="chmod calculator."
arch=('x86_64')
url="https://github.com/mirryi/cchmod"
license=('MIT')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mirryi/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('d0fb95e83ce5c3920ce517d5183e7b971f2279c3349da5b73da685c6b6b72fccaef7f5a7aa3e7f2c03052f8bfec76b58eee13426506afcc528abf237dbdb4a92')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked --all-features --target-dir=target
}

check() {
  cd "$pkgname-$pkgver"

  cargo test --release --locked --target-dir=target
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/cchmod" "$pkgdir/usr/bin/cchmod"

  install -Dm644 "README.md" "$pkgdir/usr/share/doc/${pkgname}/README.md"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
